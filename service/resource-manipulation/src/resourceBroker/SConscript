#******************************************************************
#
# Copyright 2015 Samsung Electronics All Rights Reserved.
#
#-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
#-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=

##
# ResourceBroker build script
##

Import('env')

if env.get('RELEASE'):
	env.AppendUnique(CCFLAGS = ['-Os'])
	env.AppendUnique(CPPDEFINES = ['NDEBUG'])
else:
	env.AppendUnique(CCFLAGS = ['-g'])

if env.get('LOGGING'):
	env.AppendUnique(CPPDEFINES = ['TB_LOG'])

lib_env = env.Clone()
SConscript(env.get('SRC_DIR') + '/service/third_party_libs.scons', 'lib_env')
######################### unit test setting ##########################
src_dir = lib_env.get('SRC_DIR')
gtest_dir = src_dir + '/extlibs/gtest/gtest-1.7.0'
######################################################################
resourcebroker_env = lib_env.Clone()

target_os = env.get('TARGET_OS')
######################################################################
# Build flags
######################################################################
resourcebroker_env.AppendUnique(CPPPATH = ['include'])
resourcebroker_env.AppendUnique(CPPPATH = ['../../include'])
resourcebroker_env.AppendUnique(CPPPATH = ['../common/primitiveResource/include'])
resourcebroker_env.AppendUnique(CPPPATH = ['../common/expiryTimer/include'])
resourcebroker_env.AppendUnique(CPPPATH = ['../common/expiryTimer/src'])
resourcebroker_env.AppendUnique(LIBPATH = [env.get('BUILD_DIR')])
resourcebroker_env.PrependUnique(LIBS = ['service_common', 'ExpiryTimer', 'oc', 'octbstack', 'oc_logger', 'connectivity_abstraction', 'libcoap'])


if target_os not in ['windows', 'winrt']:
    resourcebroker_env.AppendUnique(CXXFLAGS = ['-O2', '-g', '-Wall', '-fmessage-length=0', '-std=c++0x'])

if target_os == 'linux':
    resourcebroker_env.AppendUnique(LIBS = ['pthread'])

######################################################################
# Source files and Targets
######################################################################
BROKER_SRC_DIR = 'src/'
resourcebroker_src = [
        BROKER_SRC_DIR + 'DeviceAssociation.cpp',
        BROKER_SRC_DIR + 'DevicePresence.cpp',
        BROKER_SRC_DIR + 'ResourcePresence.cpp',
        BROKER_SRC_DIR + 'ResourceBroker.cpp'
        ]


if target_os in ['tizen','android'] :
    resourcebrokersdk = resourcebroker_env.SharedLibrary('ResourceBroker', resourcebroker_src)
else :
    resourcebrokersdk = resourcebroker_env.StaticLibrary('ResourceBroker', resourcebroker_src)

resourcebroker_env.InstallTarget(resourcebrokersdk, 'libResouceBroker')
resourcebroker_env.UserInstallTargetLib(resourcebrokersdk, 'libResouceBroker')
######################################################################
# Build Test
######################################################################

broker_test_env = resourcebroker_env.Clone();

broker_test_env.AppendUnique(LIBPATH = [env.get('BUILD_DIR')])
broker_test_env.AppendUnique(CCFLAGS = ['-Wnoexcept'])
broker_test_env.PrependUnique(CPPPATH = [
    env.get('SRC_DIR')+'/extlibs/hippomocks-master',
    gtest_dir + '/include'
    ])

gtest = File(gtest_dir + '/lib/.libs/libgtest.a')
gtest_main = File(gtest_dir + '/lib/.libs/libgtest_main.a')

broker_test_env.PrependUnique(LIBS = [
    'oc',
    'octbstack',
    'oc_logger',
    'connectivity_abstraction',
    'coap',
    'service_common',
    'pthread',
    'ResourceBroker',
    'ExpiryTimer',
    gtest,
    gtest_main
    ])

broker_test_src = env.Glob('unittest/*.cpp')
#broker_test_src = ['unittest/ResourcePresenceUnitTest.cpp']
broker_test = broker_test_env.Program('broker_test', broker_test_src)
Alias("broker_test", broker_test)
env.AppendTarget('broker_test')

######################################################################
# Go to build sample apps
#SConscript('SampleApp/SConscript')


