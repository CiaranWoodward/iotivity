#******************************************************************
#
# Copyright 2017 Intel Mobile Communications GmbH All Rights Reserved.
#
#-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
#-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
##
# Lyric Plugin build script
##

import os
import os.path

Import('env')

target_os = env.get('TARGET_OS')
src_dir = env.get('SRC_DIR')
bridging_path = os.path.join(src_dir, 'bridging')

lyric_env = env.Clone()

print "Reading Lyric Plugin script"

######################################################################
# Build flags
######################################################################
lyric_env.PrependUnique(CPPPATH = [ os.path.join(src_dir, 'resource', 'c_common', 'oic_malloc', 'include'),
                              os.path.join(src_dir, 'resource', 'c_common', 'oic_string', 'include'),
                              os.path.join(src_dir, 'resource', 'c_common'),
                              os.path.join(src_dir, 'resource', 'oc_logger', 'include'),
                              os.path.join(src_dir, 'resource', 'csdk', 'logger', 'include'),
                              os.path.join(src_dir, 'resource', 'csdk', 'include'),
                              os.path.join(src_dir, 'resource', 'csdk', 'stack', 'include'),
                              os.path.join(src_dir, 'extlibs', 'cjson'),
                              os.path.join(src_dir, 'extlibs', 'rapidjson', 'rapidjson', 'include', 'rapidjson')
                              ])
lyric_env.AppendUnique(CPPPATH = [ os.path.join(bridging_path, 'include'),
                             os.path.join(bridging_path, 'plugins', 'lyric_plugin'),
                             os.path.join(bridging_path, 'plugins', 'lyric_plugin', 'honeywell_objects')
                             ])

if target_os not in ['arduino', 'windows']:
    lyric_env.AppendUnique(CPPDEFINES = ['WITH_POSIX'])

if target_os in ['darwin','ios']:
    lyric_env.AppendUnique(CPPDEFINES = ['_DARWIN_C_SOURCE'])

lyric_env.AppendUnique(CXXFLAGS = ['-std=c++0x', '-Wall', '-Wextra', '-Werror'])
lyric_env.AppendUnique(RPATH = [lyric_env.get('BUILD_DIR')])
lyric_env.AppendUnique(LIBPATH = [lyric_env.get('BUILD_DIR')])

if lyric_env.get('LOGGING'):
    lyric_env.AppendUnique(CPPDEFINES = ['TB_LOG'])

lyric_env.PrependUnique(LIBS = ['m',
                               'octbstack',
                               'ocsrm',
                               'connectivity_abstraction',
                               'coap',
                               'curl' ])

#####################################################################
# Source files and Target(s)
######################################################################
lyric_src = [
         os.path.join(bridging_path, 'common', 'plugin_if.cpp'),
         os.path.join(bridging_path, 'common', 'plugin_server.cpp'),
         os.path.join(bridging_path, 'common', 'pipe_handler.cpp'),
         os.path.join(bridging_path, 'common', 'message_handler.cpp'),
         os.path.join(bridging_path, 'common', 'curl_client.cpp'),
         os.path.join(bridging_path, 'common', 'plugin_process.cpp'),
         os.path.join(bridging_path, 'common', 'ConcurrentIotivityUtils.cpp'),
         os.path.join(bridging_path, 'plugins', 'lyric_plugin', 'honeywell_resource.cpp'),
         os.path.join(bridging_path, 'plugins', 'lyric_plugin', 'honeywell_helpers.cpp'),
         os.path.join(bridging_path, 'plugins', 'lyric_plugin', 'honeywell_objects', 'honeywell_lyric.cpp'),
         os.path.join(bridging_path, 'plugins', 'lyric_plugin', 'honeywell_objects', 'honeywell.cpp'),
         os.path.join(bridging_path, 'plugins', 'lyric_plugin', 'honeywell_objects', 'honeywell_thermostat.cpp'),
         ]

lyric_env.AppendUnique(LYRIC_SRC = lyric_src)
lyriclib = lyric_env.SharedLibrary('lyricplugin', lyric_env.get('LYRIC_SRC'))
lyric_env.InstallTarget(lyriclib, 'lyricplugin')
lyric_env.UserInstallTargetLib(lyriclib, 'lyricplugin')


