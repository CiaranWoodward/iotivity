*** Settings ***
Documentation	CT1.3.8 Complete NON UPDATE Message based on CoAP
Resource	OIC_ConformanceTest_Setting.txt

*** Test Cases ***
CT1.3.8 Complete NON UPDATE Message based on CoAP
    [Documentation]					
	...	|author	|ma.hasan |		
	...	|reviewer| |				
	...	|since	|2015-05-11 |
	...	|see	|Section 8.3: UPDATE; Section 12.2.2 |
	...	|objective|Verify that the server (DUT) properly responds to a complete NON UPDATE request message from client (TE) with the entire representation of the resource |
	...	|target	|Server DUT |
	...	|test_data	|CON UPDATE Message based on CoAP |
	...	|pre_condition	|Server (DUT) is discovered from the Client (TE) |
	...	|pre_condition	|Resource properties and attribute contents are known for verification |
	...	|procedure	|1. Send a complete CON UPDATE (high QoS) request message to server from Client(TE) |
	...	|procedure	|2. Reply with UPDATE response message to client(TE) from server(DUT) |
	...	|procedure	|3. Receive UPDATE response message from server() |
        ...     |procedure      |4. Send a GET to the same resource(DUT) |
        ...     |procedure      |5. Reply with GET response |
        ...     |procedure      |6. Repeat the steps 1 through 5 |
	...	|post_condition	|None |
	...	|expected|1. OIC Layer |
	...	|expected| - fr, to, ri, rs |
	...	|expected|2. CoAP Layer |
	...	|expected| - Type = 1 (NON) |
	...	|expected| - Token = Token in request |
	...	|expected| - Resp Code = 2.04 (Content) |
	...	|expected|3. The data match the data just put |
	...	|expected|4. Same data will be written to resource |

    [Tags]	Mandatory	Server    Update
    Set Log File Directory	${TESTNAME}
    ${DUT1_ID} =	Set Variable	Server_DUT1
    ${define_dut_result}	Define DUT Information		${DUT1_ID}	${DUT1_INFO_PATH}	${DUT_TYPE_SERVER}
    Should Be True	${define_dut_result}
    @{dut1_resource_types}	Get DUT Info Value	${RESOURCE_TYPE}	${DUT1_ID}	${DISCOVERABLE}=1
    ${resource_ip}	${resource_port}	${resource_href}	Get IP Port And Uri		${RESOURCE_TYPE}=${dut1_resource_types[0]}
    @{dut1_resource_hrefs}      Get DUT Info Value      ${HREF}    ${DUT1_ID}   ${DISCOVERABLE}=1
    :FOR	${resource_href}	IN	@{dut1_resource_hrefs}
    \   ${length}    Get Length Available Representation To Update    ${DUT1_ID}      ${DISCOVERABLE}=1&${HREF}=${resource_href}
    \   Continue For Loop If    ${length} < 1
    \   Set Modified Representation Value Payload       ${DUT1_ID}      ${DISCOVERABLE}=1&${HREF}=${resource_href}
    \	${payload}	Get Json Representation
    \   ${msgid}    Generate Message Id
    \   ${token}    Generate Token
    \	${dut2_response}	Send Put Request	${UNICAST}	${COAP}	${msgid}	${token}	${resource_ip}	${${resource_port}}	${resource_href}	${NON_CONFIRMABLE}	${payload}
    \	Should Not Be Empty		${dut2_response}
    \	${dut2_response_json}	Get Response Json	${dut2_response}
    \	${validate_json_result}	Validate JSON Format        ${dut2_response_json}
    \	Should Be True	${validate_json_result}
    \	${duty_response_type}	Get Response Type	${dut2_response}
    \	Should Be Equal	${duty_response_type}	${NON}
    \	${dut2_response_token}   Get Response Token       ${dut2_response}
    \	Should Be Equal             ${dut2_response_token}    ${token}
    \	${dut2_response_code}	Get Response Code       ${dut2_response}
    \	Should Be Equal	${COAP_RESPONSE_CODE_UPDATED}	${dut2_response_code}
    \	${dut2_payload}	Get Response Json	${dut2_response}
    \   Check Modified Representation Value     ${DUT1_ID}      ${DISCOVERABLE}=1&${HREF}=${resource_href}      ${dut2_payload}
    Release DUT information     ${DUT1_ID}
