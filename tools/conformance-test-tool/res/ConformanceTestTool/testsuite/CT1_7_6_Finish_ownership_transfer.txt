*** Settings ***
Documentation	CT1.7.6 Finish ownership transfer
Resource	OIC_ConformanceTest_Setting.txt


*** Test Cases ***
CT1.7.5.6 Finish ownership transfer
    [Documentation]					
	...	|author	| |	
	...	|reviewer|	|			
	...	|since	|2015-10-09 |
	...	|see	|Security Spec Section 7.3 Device Ownership Transfer Methods |
	...	|objective	|Verify the Server (DUT) change the owned status of ‘doxm' resource to ‘TRUE’.   |
	...	|target	|Server DUT |
	...	|test_data| “/oic/sec/doxm.owned” == “True”  |
	...	|test_configuration | None |
	...	|pre_condition|Any one of three method is successfully finished. |
	...	|pre_condition|    Option 1) Just-Works:: CT1.7.5.1 is successfully finished. |
	...	|pre_condition|    Option 2) Random Pin based: CT1.7.5.2 is successfully finished |
	...	|pre_condition|    Option 3) Manufacturer Certificate Based: CT1.7.2.3 is successfully finished. |
	...	|procedure| 1. Test Application initiates a PUT request to /oic/sec/doxm resource in Server (DUT) to change ‘Owned’ property and ‘Owner’ |	
	...	|procedure| 2. DUT sends response of PUT req. to doxm |
	...	|post_condition|None |
	...	|expected| 1. Client (TE) get RSP 2.04  |
	...	|expected| 2. Server (DUT) “/oic/sec/doxm.owned” == “True” |
	...	|expected| 3. Server (DUT) “/oic/sec/doxm.DevOwner” == Device ID of the Client (TE) |

    [Tags]	Mandatory	Security
    
    #Precondition
    ${server_type}	Wait For User Confirmation	Please Select Server Type	${JUST_WORKS_SERVER} ${RANDOM_PIN_SERVER}
    
    ${SECURITY_CLIENT_DIR}	Get Security Client Simulator Directory
    Initialize Device Controller	${DEVICE_ID}
    Change Working Directory	${SECURITY_CLIENT_DIR}
    Execute Application	${SECURITY_CLIENT_PROCESS_ID}	${SECURITY_CLIENT}
    ConformanceKeyword.Log to Console	Starting Security Provisioning Client ...
    Start Application Log	${DEVICE_ID}	${SECURITY_CLIENT_PROCESS_ID}
    Sleep	5 seconds
    
    #Main Body
    ConformanceKeyword.Log to Console	Discovering Unowned Devices ...
    Input Command To Application	${SECURITY_CLIENT_PROCESS_ID}	${INPUT_DISCOVERY_UNOWNED_DEVICE}
    Sleep	5 seconds
    ${current_time}	Get Current Date
    ConformanceKeyword.Log to Console	Transferring Ownership ...
    Input Command To Application	${SECURITY_CLIENT_PROCESS_ID}	${INPUT_OWNER_TRANSFER}
    Sleep	3 seconds
    Input Command To Application	${SECURITY_CLIENT_PROCESS_ID}	${INPUT_DEVICE_SELECTION}
    
    Run Keyword if	'${server_type}' == '${RANDOM_PIN_SERVER}'	Take Random Pin From User	${SECURITY_CLIENT_PROCESS_ID}
    
    Sleep	5 seconds
    Input Command To Application	${SECURITY_CLIENT_PROCESS_ID}	${INPUT_CLOSE_SECURITY_CLIENT}
    ${app_log}	Get Application Log	${DEVICE_ID}	${SECURITY_CLIENT_PROCESS_ID}
    Should Contain	${app_log}	${OWNER_TRANSFER_SUCCESS_MESSAGE}
    ConformanceKeyword.Log to Console	${app_log}

    ${dut_response}	Get Multicast Discovery Request List	${COAP}	${OIC_CORE_PROVISION_URI}	${PROVISION_QUERY_OWNED}
    ${dut_response_code}	${resource_ip}	${resource_port}	${response_payload}	Get Response Code IP Port And Payload From Response	${dut_response}	
	Should Be Equal         ${COAP_RESPONSE_CODE_SUCCESS}   ${dut_response_code}
	${message_id}	Generate Message Id
    ${token}	Generate Token
    ${dut_get_response}        Send Get Request        ${UNICAST}      ${COAP} 	${message_id}	${token}	${resource_ip}	  ${${resource_port}}	${OIC_CORE_PROVISION_URI}	${CONFIRMABLE}
    Should Not Be Empty             ${dut_get_response}
    ${dut_get_response_code}   Get Response Code       ${dut_get_response}
    Should Be Equal         ${COAP_RESPONSE_CODE_SUCCESS}   ${dut_get_response_code}
    ${dut_get_response_payload}	Get Response Payload	${dut_get_response}
    ${owned_status}	Get Owned Status From Response	${dut_get_response_payload}
    Should Be Equal	${owned_status}    true
    ${device_owner}    Get Device Owner From Response	${dut_get_response_payload}
    Should Be Equal    ${device_owner}     ${PROVISION_CLIENT_DI}
