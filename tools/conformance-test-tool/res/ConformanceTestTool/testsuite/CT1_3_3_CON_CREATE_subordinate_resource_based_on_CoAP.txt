*** Settings ***
Documentation	CT1.3.3 CON CREATE subordinate resource based on CoAP
Resource	OIC_ConformanceTest_Setting.txt

*** Test Cases ***
CT1.3.3 CON CREATE subordinate resource based on CoAP
    [Documentation]					
	...	|author	| |		
	...	|reviewer| |				
	...	|since	|2015-05-11 |
	...	|see	|Section 8.1: CREATE; Section 12.2.2|
	...	|objective|Verify that the server (DUT) properly responds to CON CREATE subordinate resource message from client (TE) |
	...	|target	|Server DUT | 
	...	|test_data|CON CREATE subordinate resource based on CoAP |
	...	|pre_condition	|Server (DUT) is discovered from the Client (TE) | 
	...	|pre_condition	|A resource collection exists on server |
	...	|pre_condition	|Resource properties and attribute contents are known for verification |
	...	|procedure	|1. Send CON (high QoS) CREATE request message to create a subordinate resource on server |
	...	|procedure	|2. Reply with CREATE response message to client |
	...	|procedure	|3. Receive CREATE response message from server and return an Ack|
	...	|post_condition	|Remove created resource |
	...	|expected|1. OIC Layer |
	...	|expected| -  fr, to, ri, cn. rs |
	...	|expected| -  cn includes the URI and the resource representation of the created resource |
	...	|expected|2. CoAP Layer |
	...	|expected| - Type = 3 (Ack) |
	...	|expected| - MsgId = MsgId in request |
	...	|expected| - Token = Token in request |
	...	|expected| - Resp Code = 2.01 (Created) |

    [Tags]	Mandatory	Server	Discovery
#Pre-Condition
    Set Log File Directory	${TESTNAME}
    ${DUT1_ID}	Set Variable	Server_DUT1
    ${define_dut_result}	Define DUT Information		${DUT1_ID}	${DUT1_INFO_PATH}	${DUT_TYPE_SERVER}
    Should Be True	${define_dut_result}
    @{dut1_resource_types}	Get DUT Info Value	${RESOURCE_TYPE}	${DUT1_ID}	${DISCOVERABLE}=1
    :FOR       ${dut1_resource_type}	IN	@{dut1_resource_types}
    \	${resource_ip}	${resource_port}	${resource_href}	Get IP Port And Uri		${RESOURCE_TYPE}=${dut1_resource_type}
#Test-Body
    \	Create Json Representation
    \	Add Into Json Representation	${RESOURCE_TYPE}	${dut1_resource_type}
    \	Add Into Json Representation    ${HREF}		${SUBORDINATE_HREF}
    \   ${msgid}    Generate Message Id
    \   ${token}    Generate Token
    \	${payload}	Get Json Representation
    \	${dut2_response}	Send Post Request	${UNICAST}	${COAP}    ${msgid}    ${token}    ${resource_ip}    ${${resource_port}}    ${resource_href}    ${CONFIRMABLE}    ${payload}
    \	Should Not Be Empty		${dut2_response}
    \	${duty_response_type}	Get Response Type	${dut2_response}
    \	Should Be Equal	${duty_response_type}	${ACK}
    \	${dut2_response_code}	Get Response Code	${dut2_response}
    \	Should Be Equal		${COAP_RESPONSE_CODE_CREATED}	${dut2_response_code}
    \	${dut2_response_token}   Get Response Token       ${dut2_response}
    \	Should Be Equal            ${dut2_response_token}    ${token}
    \	${dut2_response_msgid}   Get Response Msgid       ${dut2_response}
    \	Should Be Equal             ${dut2_response_msgid}    ${msgid}
    \	${dut2_response_json}	Get Response Json	${dut2_response}
    \	@{dut2_response_href}	Get Json Value	${HREF}	${dut2_response_json}
    \	Should Be Equal		@{dut2_response_href}[0]		${resource_href}${SUBORDINATE_HREF}
#Post-Condition
    \   ${msgid}    Generate Message Id
    \   ${token}    Generate Token
    \	${dut2_response}	Send Delete Request		${UNICAST}	${COAP}	   ${msgid}    ${token}    ${resource_ip}    ${${resource_port}}	${resource_href}${SUBORDINATE_HREF}		${CONFIRMABLE}
    \	Should Not Be Empty		${dut2_response}
    \	${dut2_response_json}	Get Response Json	${dut2_response}
    \	${validate_json_result}		Validate JSON Format	${dut2_response_json}
    \	Should Be True	${validate_json_result}
    \	${dut2_response_code}	Get Response Code	${dut2_response}
    \	Should Be Equal		${COAP_RESPONSE_CODE_DELETED}	${dut2_response_code}
    Release DUT information		${DUT1_ID}

