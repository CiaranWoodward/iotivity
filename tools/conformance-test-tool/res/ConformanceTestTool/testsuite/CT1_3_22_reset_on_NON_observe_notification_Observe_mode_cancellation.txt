*** Settings ***
Documentation	CT1.3.22 RESET on NON observe notification - Observe mode cancellation
Resource	OIC_ConformanceTest_Setting.txt
Test Setup    Pre Condition
Test TearDown    Post Condition

*** Test Cases ***
CT1.3.22 RESET on NON observe notification - Observe mode cancellation
    [Documentation]					
	...	|author	| |		
	...	|reviewer| |				
	...	|since	|2015-06-18|
	...	|see	|IETF draft-ietf-core-observe-16, Section 3.6 |
	...	|objective|Verify the server (DUT) de-registers client from observer list if client (TE) sends a RESET upon receiving a NON observe notification from the server |
	...	|target	||
	...	|test_data|NON CONFIRMABLE = 1 |
	...	|pre_condition	|1. Server (DUT) is discovered from the Client (TE)|
	...	|pre_condition	|2. Resource properties and attribute contents are known for verification |
	...	|pre_condition	|3. Server (DUT) is in observe mode |
	...	|procedure	|1. Observe condition satisfied |
	...	|procedure	|2. Reply with NON RETRIEVE response message to client |
	...	|procedure	|3. Upon receiving the response message, send a RESET message with the same token back |
	...	|procedure	|4. Cancel observation mode |
	...	|procedure	|5. Observe condition satisfied |
	...	|post_condition	| |
	...	|expected|1. Client does not receive any RETRIEVE Response messages with observe indication |
	
    [Tags]	Mandatory	Server    Observe
    Set Log File Directory	${TESTNAME}

    @{dut1_resource_hrefs}       Get DUT Info Value      ${HREF}    ${DUT1_ID}      ${OBSERVABLE}=1&${DISCOVERABLE}=1
    :FOR        ${resource_href}   IN      @{dut1_resource_hrefs}
    \   ${msgid1}    Generate Message Id
    \   ${token1}    Generate Token
    \	${dut2_response}    Send Observe Request	${COAP}	${msgid1}	${token1}	${SERVER_IP}    ${${SERVER_PORT}}    ${resource_href}	${NON_CONFIRMABLE}
    \   Should Not Be Empty    ${dut2_response}    No response found for observe request
    \	${duty_response_type}	Get Response Type	${dut2_response}
    \	Should Be Equal    ${duty_response_type}    ${NON}    Observe response type is not NON
    \   ${dut2_response_code}   Get Response Code       ${dut2_response}
    \   Should Be Equal         ${COAP_RESPONSE_CODE_RETRIEVED}         ${dut2_response_code}     Response code mismatch for observe request
    \	${dut2_response_token}   Get Response Token       ${dut2_response}
    \	Should Be Equal             ${dut2_response_token}    ${token1}    Response token mismatch for observe request
    \	${dut2_response_msgid}   Get Response Msgid       ${dut2_response}
    \	Should Be Equal             ${dut2_response_msgid}    ${msgid1}    Response message id mismatch for observe request

    \   ${length}    Get Length Available Representation To Update    ${DUT1_ID}      ${DISCOVERABLE}=1&${HREF}=${resource_href}
    \   Run Keyword If    ${length} > 0    Set Modified Partial Representation Value Payload       ${DUT1_ID}      ${OBSERVABLE}=1&${HREF}=${resource_href}&${DISCOVERABLE}=1
    \   ${payload}    Run Keyword If    ${length} > 0    Get Json Representation
    \   ${msgid2}    Generate Message Id
    \   ${token2}    Generate Token
    \	${update_support}    Is Crudn Operation Supported    ${resource_href}    ${PARTIAL_UPDATE}
    \   Clear Notifications     ${COAP}
    \   ${dut2_response}    Run Keyword If    ${length} > 0 and ${update_support} == ${True}    Send Post Request        ${UNICAST}      ${COAP}    ${msgid2}       ${token2}       ${SERVER_IP}    ${${SERVER_PORT}}     ${resource_href}        ${NON_CONFIRMABLE}      ${payload}
    \   Run Keyword If    ${length} < 1 or ${update_support} == ${False}    Log To Console    ============== User Control -> Change state of resource for notification================
    \   Builtin.sleep   ${${DEFAULT_WAIT_TIME}}
    \	${dut2_response}	Get Observe Response	 ${COAP}    ${resource_href}
    \	Should Not Be Empty		${dut2_response}    Pre condition failed: Observe notification did not reach to client

    \	Cancel Observe Reset	${COAP}    ${resource_href}

    \   Update Representation Multiple Times    ${SERVER_IP}    ${SERVER_PORT}    ${resource_href}    ${NON_CONFIRMABLE}    ${payload}    ${update_support}    ${${NON_UPDATE_REQUIREMENT_RST}}

    \   Builtin.sleep   ${${DEFAULT_WAIT_TIME}}
    \   Clear Notifications     ${COAP}

    \   ${msgid8}    Generate Message Id
    \   ${token8}    Generate Token
    \   ${dut2_response}    Run Keyword If    ${length} > 0 and ${update_support} == ${True}    Send Post Request        ${UNICAST}      ${COAP}    ${msgid8}       ${token8}       ${SERVER_IP}    ${${SERVER_PORT}}     ${resource_href}        ${NON_CONFIRMABLE}      ${payload}
    \   Run Keyword If    ${length} < 1 or ${update_support} == ${False}    Log To Console    ============== User Control -> Change state of resource for notification================
    \   Builtin.sleep   ${${DEFAULT_WAIT_TIME}}

    \	${dut2_response}	Get Observe Response	 ${COAP}    ${resource_href}
    \	Should Be Empty		${dut2_response}    Notification for Observe received even after cancelling the observation by Reset message

*** Keywords ***

Pre Condition
    Start Capture
    Set Log File Directory	${TESTNAME}
    Set Global Variable    ${DUT1_ID}    Server_DUT1
    ${define_dut_result}	Define DUT Information		${DUT1_ID}	${DUT1_INFO_PATH}	${DUT_TYPE_SERVER}
    Should Be True	${define_dut_result}    The DUTDescriptor has some problem!!
    @{discoverable_resources}	Get DUT Info Value	${RESOURCE_TYPE}	${DUT1_ID}    ${DISCOVERABLE}=1
    ${resource_ip}	${resource_port}	${resource_href}	Get IP Port And Uri		${RESOURCE_TYPE}=@{discoverable_resources}[0]
    Set Global Variable    ${SERVER_IP}    ${resource_ip}
    Set Global Variable    ${SERVER_PORT}    ${resource_port}

Post Condition
    @{observable_resource_hrefs}	Get DUT Info Value	${HREF}	   ${DUT1_ID}	${OBSERVABLE}=1
    :FOR        ${resource_href}   IN      @{observable_resource_hrefs}
    \   ${msgid1}    Generate Message Id
    \	${dut2_response}	Cancel Observe With Get Message    ${COAP}    ${msgid1}    ${SERVER_IP}    ${${SERVER_PORT}}    ${resource_href}    ${CONFIRMABLE}
    Release DUT information     ${DUT1_ID}
    End Capture
